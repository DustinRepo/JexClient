plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id "com.github.johnrengelman.shadow" version "7.1.2"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	mavenCentral()
}

dependencies {
	def dependency = { id ->
		implementation id
		shadow id
	} as Object
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	dependency group: 'pircbot', name: 'pircbot', version: '1.5.0'
	dependency group: 'org.yaml', name: 'snakeyaml', version: '1.30'
	dependency files('lib/IRCClient.jar')
	implementation group: 'io.netty', name: 'netty-handler-proxy', version: '4.1.76.Final'



	implementation files('lib/baritone-1.6.3-dev.jar')
	implementation files('lib/sodium.jar')
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

remapJar {
	input.set(shadowJar.archiveFile)
	archiveName = "JexClient-Snap.jar"
}

shadowJar {
	archiveName = "shadow-unmapped.jar"
	configurations = [project.configurations.shadow]
	exclude("com/google/")
}
